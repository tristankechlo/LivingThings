import groovy.json.JsonOutput
import groovy.json.JsonSlurper

subprojects {
    apply plugin: 'java'
    java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)

    jar {
        from rootProject.file('CREDITS.md')
        from rootProject.file('LICENSE')
        manifest {
            attributes([
                    'Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : project.jar.archiveVersion,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : project.jar.archiveVersion,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestamp'               : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'BlameJared Maven (JEI / CraftTweaker / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    processResources {
        // replace placeholders
        def expandProps = [
                "version"                   : "${minecraft_version}-${mod_version}",
                "version_range"             : version_range,
                "minecraft_version"         : minecraft_version,
                "java_version"              : java_version,
                "fabric_loader_min"         : fabric_loader_min,
                "fabric_range_min_inclusive": fabric_range_min_inclusive,
                "fabric_range_max_exclusive": fabric_range_max_exclusive,
                "forge_loader_range"        : forge_loader_range
        ]
        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
            expand expandProps
        }
        inputs.properties(expandProps)

        // minify json files
        doLast {
            fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
                File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            }
        }
    }
}
